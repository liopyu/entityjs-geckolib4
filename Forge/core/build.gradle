plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroup "com.eliotlash.mclib"
        }
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}

sourcesJar {
    //Include the license into the sourcesJar
    from(rootProject.file("LICENSE"))
}

javadocJar {
    //Include the license into the sourcesJar
    from(rootProject.file("LICENSE"))
}

dependencies {
    implementation 'com.eliotlash.mclib:mclib:19'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'it.unimi.dsi:fastutil:8.4.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
}

jar {
    from(rootProject.file("LICENSE"))
    manifest {
        attributes([
                'Specification-Title'     : project.name,
                'Specification-Vendor'    : authors,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/geckolib3/geckolib/"
            def releasesRepoUrl = "https://maven.cloudsmith.io/geckolib3/geckolib/"
            def snapshotsRepoUrl = "https://maven.cloudsmith.io/geckolib3/geckolib/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                def envUsername = System.getenv("cloudUsername")
                def envPassword = System.getenv("cloudPassword")
                username = envUsername == null ? findProperty("cloudUsername") : envUsername
                password = envPassword == null ? findProperty("cloudPassword") : envPassword
            }
        }
        //Commented out, but uncomment and run geckolib-core:publishMavenJavaPublicationToMavenLocalRepository
        //to see how the published artifact looks, artifact id, group, version metadata...
        //Result is in your maven local, %USER_DIR%/.m2 usually.
        mavenLocal()
    }
}